group = "dev.jonathanvegasp.result_channel"
version = "1.0.0"

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "dev.jonathanvegasp.result_channel"

    compileSdk = 35
    ndkVersion = "27.2.12479018"

    externalNativeBuild {
        cmake {
            path = "../src/CMakeLists.txt"
        }
    }

    buildFeatures {
        prefabPublishing true
    }

    prefab {
        create("result_channel") {
            headers "../src/include"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
        externalNativeBuild {
            cmake {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"

                arguments "-Wl,--no-undefined",
                        "-Wl,--exclude-libs,ALL",
                        "-Wl,--strip-all",
                        "-Wl,--gc-sections",
                        "-Wl,--as-needed",
                        "-Wl,-z,relro",
                        "-Wl,-z,now",
                        "-Wl,-z,nocopyreloc",
                        "-Wl,--icf=all",
                        "-Wl,--hash-style=gnu",
                        "-Wl,--sort-section=name",
                        "-Wl,--sort-common",
                        "-Wl,--no-gnu-unique",
                        "-Wl,--no-ident",
                        "-Wl,--no-undefined-version",
                        "-Wl,--relax",
                        "-Wl,--build-id=none",
                        "-Wl,--no-keep-memory",
                        "-Wl,--no-rosegment",
                        "-DANDROID_STL=none"

                cppFlags "-O2",
                        "-flto",
                        "-ffunction-sections",
                        "-fdata-sections",
                        "-fvisibility=hidden",
                        "-DNDEBUG",
                        "-g0",
                        "-fno-exceptions",
                        "-fno-rtti",
                        "-fno-unwind-tables",
                        "-fno-asynchronous-unwind-tables",
                        "-fno-stack-protector",
                        "-fno-threadsafe-statics",
                        "-fno-use-cxa-atexit",
                        "-nostdlib++",
                        "-nostdinc++",
                        "-fno-ident",
                        "-fno-stack-clash-protection",
                        "-fno-stack-check",
                        "-fno-math-errno",
                        "-fmerge-constants",
                        "-fno-keep-inline-functions",
                        "-fno-keep-static-consts"
            }
        }
    }

    packagingOptions {
        jniLibs {
            excludes += ['**/libresult_channel.so', '**/libc++_shared.so']
        }
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.18.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

tasks.configureEach { task ->
    def dir = file("${projectDir}/.cxx")

    if (!dir.exists()) {
        task.outputs.upToDateWhen { false }
    }
}

tasks.named('clean') {
    delete "${projectDir}/.cxx"
}
